;Amazon Info Retriever 1.2
;by Soul_Eater (C) 2009
;Currently this snippet retrieves a full image or statistics for a book or dvd/blu-ray listing
;Use 'dvd' if it is a dvd or blu-ray
;Syntax:
;/amazon [book|image|dvd] amazon url
;Example: /amazon dvd http://www.amazon.com/Charlie-Brown-Christmas-Blu-ray/dp/B002HQZX9I/ref=sr_1_60?ie=UTF8&s=dvd&qid=1248414359&sr=1-60/
;Example: /amazon image http://www.amazon.com/Charlie-Brown-Christmas-Blu-ray/dp/B002HQZX9I/ref=sr_1_60?ie=UTF8&s=dvd&qid=1248414359&sr=1-60/

alias amazon {
  unset %am.*
  if ($sock(amazon)) { sockclose amazon }
  set %am.x $1
  if (*amazon.com* iswm $2-) {
    if (*dp* iswm $2-) || (*gp* iswm $2-) { set %am.y $gettok($2-,3-5,47) }
    set %am.z (<font color=#990000
    set %am.ticks $ticks
    sockopen amazon www.amazon.com 80
    echo -a Connecting to Amazon.com
    echo -a -
  }
  else { echo -a Please enter an Amazon url }
}

on *:SOCKREAD:amazon: {
  if ($sockerr > 0) { echo -a Error in sockread | return }
  sockread -f %sockread
  if ($sockbr == 0) { return } 
  if (%am.x != image) && (*<title>* iswm %sockread) { tokenize %sockread 58 | echo -a $+(,$gettok(%sockread,2,58),) }
  if (%am.x != image) && (*<b class="priceLarge">* iswm %sockread) { tokenize %sockread 62 | echo -a Price: $remove($gettok(%sockread,3,62),</b) } 
  if (%am.x == book) && (*<strong>This item:* iswm %sockread) { tokenize %sockread 59 | echo -a Author: $remove($remove($gettok(%sockread,3,59),</span>),by) }
  if (%am.x == image) && (*"original_image",* iswm %sockread) { tokenize %sockread 32 | echo -a $+($gettok($remove($gettok(%sockread,2,32),"),1,95),jpg) | endamazon  }
  if (%am.x == dvd) && (*<b>Directors:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Director(s): $remove($gettok(%sockread,5,62),</a) }
  if (%am.x == dvd) && (*<li> <b>Format:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Format: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li><b>Language:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Language: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li><b>Subtitles:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Subtitles: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li> <b>Region:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Region: $remove($remove($gettok(%sockread,4,62),%am.z),</li) }
  if (%am.x == dvd) && (*<b>Aspect Ratio:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Aspect Ratio: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li> <b>Number of discs:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Number of Discs: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li> <b>Rating: </b>* iswm %sockread) { tokenize %sockread 62 | echo -a Rating: $remove($gettok(%sockread,6,62),</span) }
  if (%am.x == dvd) && (*<li> <b>Studio:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Studio: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li> <b>DVD Release Date:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Release Date: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == dvd) && (*<li> <b>Run Time:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Run Time: $remove($gettok(%sockread,4,62),</li) | endamazon  }
  if (%am.x == book) && (*<li><b>Reading level:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Reading Level: $remove($gettok(%sockread,4,62),<br /) }
  if (%am.x == book) && (*pages</li>* iswm %sockread) { tokenize %sockread 62 | echo -a Pages: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == book) && (*<li><b>Publisher:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Publisher: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == book) && (*<li><b>Language:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a Language: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == book) && (*<li><b>ISBN-10:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a ISBN-10: $remove($gettok(%sockread,4,62),</li) }
  if (%am.x == book) && (*<li><b>ISBN-13:</b>* iswm %sockread) { tokenize %sockread 62 | echo -a ISBN-13: $remove($gettok(%sockread,4,62),</li) | endamazon  }
}

ON *:SOCKOPEN:amazon:{
  if ($sockerr > 0) {
    echo -a $sock($sockname).wsmsg
    return
  }
  sockwrite -n $sockname GET $+(/,%am.y) HTTP/1.1
  sockwrite -n $sockname Host: www.amazon.com
  sockwrite -n $sockname User-Agent: Mozilla/4.0 (compatible; MSIE 6.0; Windows NT 5.1; SV1; .NET CLR 1.1.4322; FDM)
  sockwrite -n $sockname Accept: *.*, */*
  sockwrite -n $sockname Content-Type: text/html
  sockwrite -n $sockname $crlf
}

alias endamazon {
  if ($sock(amazon)) { sockclose amazon }
  echo -a -
  var %t $calc(($ticks - %am.ticks) / 1000)
  echo -a  -> Query Completed in %t $+ secs...
  unset %am.*
}

alias amazons {
  unset %am.*
  if ($sock(amazons)) { sockclose amazons }
  set %am.x $replace($1-,$chr(32),+)
  set %am.ticks $ticks
  sockopen amazons www.amazon.com 80
  echo -a Connecting to Amazon.com
  echo -a -
}

on *:SOCKREAD:amazons: {
  if ($sockerr > 0) { echo -a Error in sockread | return }
  sockread -f %sockread
  if ($sockbr == 0) { return } 
  if (*<div class="productImage">* iswm %sockread) {
    tokenize %sockread 32
    var %tmp $gettok(%sockread,5,32)
    tokenize %tmp 46
    echo -a $+($remove($gettok(%tmp,1-3,46),src="),.jpg)
  }
  if (*<div class="productTitle"* iswm %sockread) { 
    var %rem <a href="
    var %rem2 )
    tokenize %sockread 62 
    ;url
    echo -a $remove($remove($gettok(%sockread,2,62),%rem),") 
    ;title
    echo -a $remove($gettok(%sockread,3,62),</a)
    ;type
    if ($remove($gettok(%sockread,8,62),</span) == $null) { echo -a null } else { echo -a $remove($gettok(%sockread,8,62),</span) }
    ;date
    echo -a $remove($remove($remove($gettok(%sockread,9,62),</span),-),%rem2)
  }
  if (*<div class="newPrice">* iswm %sockread) { 
    tokenize %sockread 62
    echo -a $remove($gettok(%sockread,7,62),</span)
    echo -a ****
  }
}
;//var %x <div class="productTitle"><a href="http://www.amazon.com/Dead-Man-Campus-Everett-Scott/dp/6305310254/ref=sr_1_1/181-0787784-5050049?ie=UTF8&qid=1248827089&sr=8-1"> Dead Man on Campus</a> <span class="binding"> ~ Tom Everett Scott, Mark-Paul Gosselaar, and Poppy Montgomery</span><span class="binding"> (<span class="format">DVD</span> - Mar 9, 1999)</span></div> | tokenize %x 62 | echo -a $gettok(%x,3,62)

ON *:SOCKOPEN:amazons:{
  if ($sockerr > 0) {
    echo -a $sock($sockname).wsmsg
    return
  }
  sockwrite -n $sockname GET $+(/s/ref=nb_ss_gw?url=search-alias%3Daps&field-keywords=,%am.x,&x=0&y=0) HTTP/1.1
  sockwrite -n $sockname Host: www.amazon.com
  sockwrite -n $sockname User-Agent: Mozilla/4.0 (compatible; MSIE 6.0; Windows NT 5.1; SV1; .NET CLR 1.1.4322; FDM)
  sockwrite -n $sockname Accept: *.*, */*
  sockwrite -n $sockname Content-Type: text/html
  sockwrite -n $sockname $crlf
}

alias endamazons {
  if ($sock(amazons)) { sockclose amazons }
  echo -a -
  var %t $calc(($ticks - %am.ticks) / 1000)
  echo -a  -> Query Completed in %t $+ secs...
  unset %am.*
}
