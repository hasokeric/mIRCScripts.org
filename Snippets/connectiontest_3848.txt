; --------------------------------------
; Connectiontest - by Flobse
; 
; This socketscript will connect to a specifiied website and returns the results of connection.
; For example if the site can be reached, sitetitle or location, or what is the error and so on.
; Mainly just to test if a site exists.
;
; --------------------------------------
; Syntax: /connectiontest <#Channel|Nick|Status Window|@Window> <URL>[:Port]
;
; --------------------------------------
; Example: 
;
; /connectiontest $active www.flobse.de/index.html
;  will return: Connectiontest to "www.flobse.de/index.html" successful. (Title: Flobse.de (47ms))
;
; /connectiontest Status Window http://tinyurl.com/2gkxxk
;  will return: Connectiontest to "http://tinyurl.com/2gkxxk" successful. (Forwarding to: http://www.mircscripts.org/index.php (47ms))
;
; --------------------------------------
; ## #Flobse @ Quakenet - Scripter@Flobse.de - UIN: 152759920 ##


alias connectiontest {
  if ($regex($2-,/(?:window\s)?(?:http://)?((?:www.)?[^/|^:]+)(/[^:]+)?(?::(\d+))?/gi)) {
    if (($me ison $1) || ($1-2 == Status Window) || ($left($1,1) == @) || ($query($1))) { sockclose Connectiontest | sockopen Connectiontest $regml(1) $iif($regml(3),$regml(3),80) | sockmark Connectiontest $ticks $replace($1-,Status Window,-s) }
    else { echo $color(info) -s * /connectiontest target error. Syntax: /connectiontest target url }
  }
  else {
    if ($isid) { return $false }
    elseif ($me ison $1) { msg $1 " $+ $2 $+ " isnt a correct URL. Syntax: /connectiontest target url }
    else { echo $color(info)-s * /connectiontest " $2 $+ " isnt a correct URL. Syntax: /connectiontest target url }
  }
}

on *:sockopen:connectiontest: {
  tokenize 32 $sock($sockname).mark | noop $regex($3-,/(?:window\s)?(?:http://)?((?:www.)?[^/|^:]+)(/[^:]+)?(?::(\d+))?/i) | var %s sockwrite -n $sockname
  if ($sockerr) { $iif($query($2) || $me ison $2,msg,echo) $2 Connectiontest to " $+ $remove($iif($4 && $3 != /,$2-3,$3),$chr(32)) $+ " 4not successful. (Host $+(",$regml(1),") $iif($regml(3),on port $regml(3)) not found.) | return } 
  %s GET $iif($regml(2),$v1,/) HTTP/1.1 | %s Host: $regml(1) | %s
}

on *:sockread:connectiontest: { 
  var %t | sockread %t | tokenize 32 $sock($sockname).mark | noop $regex(%t,/HTTP\S* (\d+).*/gi) | noop $regex(info,$3-,/(?:window\s)?(?:http://)?((?:www.)?[^/|^:]+)(/[^:]+)?(?::(\d+))?/i)
  if ((!%t) && (%connectiontest)) { sockread %t | if (!%t) { $iif($query($2) || $me ison $2,msg,echo) $2 Connectiontest to " $+ $remove($iif($5 && $4 != /,$3-4,$3),$chr(32)) $+ " successful. (Server found, no file. ( $+ $calc($ticks - $1) $+ ms)) | sockclose $sockname } }
  elseif ($regml(1) >= 400) { $iif($query($2) || $me ison $1,msg,echo) $2 Connectiontest to " $+ $remove($iif($5 && $4 != /,$3-4,$3),$chr(32)) $+ " 4not successful. (File " $+ $regml(info,2) $+ " not found. ( $+ $calc($ticks - $1) $+ ms)) | sockclose $sockname }
  elseif ($regml(1)) { set %connectiontest $regml(1) }
  elseif ($regex(info,%t,/\s*(?:(?:.*<Title>)?\s*(.+)</(title)>\s*|\s*(Location):\s*(.+))/i)) { $iif($query($2) || $me ison $2,msg,echo) $2 Connectiontest to " $+ $remove($iif($5 && $4 != /,$3-4,$3),$chr(32)) $+ " successful. ( $+ $htmldecode($replace($regml(info,$iif($istok(location title,$regml(info,1),32),1,2)),Location,Forwarding to,title,Title) $+ : $regml(info,$iif($istok(location title,$regml(info,1),32),2,1))) ( $+ $calc($ticks - $1) $+ ms)) | sockclose $sockname } 
  elseif ($regex(%t,\s*</html>\s*)) { $iif($query($2) || $me ison $2,msg,echo) $2 Connectiontest to " $+ $remove($iif($5 && $4 != /,$3-4,$3),$chr(32)) $+ " successful. ( $+ $calc($ticks - $1) $+ ms) | sockclose $sockname } 
}

alias -l htmldecode {
  var %r = $replacecs($1,&quot;,$chr(34),&apos;,$chr(39),&amp;,$chr(38),&lt;,$chr(60),&gt;,$chr(62),&nbsp;,$chr(160),&iexcl;,$chr(161),&curren;,$chr(164),&cent;,$chr(162),&pound;,$chr(163))
  var %r = $replacecs(%r,&yen;,$chr(165),&brvbar;,$chr(166),&sect;,$chr(167),&uml;,$chr(168),&copy;,$chr(169),&ordf;,$chr(170),&laquo;,$chr(171),&not;,$chr(172),&shy;,$chr(173),&reg;,$chr(174))
  var %r = $replacecs(%r,&trade;,$chr(8482),&macr;,$chr(175),&deg;,$chr(176),&plusmn;,$chr(177),&sup2;,$chr(178),&sup3;,$chr(179),&acute;,$chr(180),&micro;,$chr(181),&para;,$chr(182),&yuml;,$chr(255))
  var %r = $replacecs(%r,&middot;,$chr(183),&cedil;,$chr(184),&sup1;,$chr(185),&ordm;,$chr(186),&raquo;,$chr(187),&frac14;,$chr(188),&frac12;,$chr(189),&frac34;,$chr(190),&iquest;,$chr(191))
  var %r = $replacecs(%r,&times;,$chr(215),&divide;,$chr(247),&Agrave;,$chr(192),&Aacute;,$chr(193),by Flobse,by Flobse,&Acirc;,$chr(194),&Atilde;,$chr(195),&Auml;,$chr(196),&Aring;,$chr(197),&AElig;,$chr(198))
  var %r = $replacecs(%r,&Ccedil;,$chr(199),&Egrave;,$chr(200),&Eacute;,$chr(201),&Ecirc;,$chr(202),&Euml;,$chr(203),&Igrave;,$chr(204),&Iacute;,$chr(205),&Icirc;,$chr(206),&Iuml;,$chr(207))
  var %r = $replacecs(%r,&ETH;,$chr(208),&Ntilde;,$chr(209),&Ograve;,$chr(210),&Oacute;,$chr(211),&Ocirc;,$chr(212),&Otilde;,$chr(213),&Ouml;,$chr(214),&Oslash;,$chr(216),&Ugrave;,$chr(217))
  var %r = $replacecs(%r,&Uacute;,$chr(218),&Ucirc;,$chr(219),&Uuml;,$chr(220),&Yacute;,$chr(221),&THORN;,$chr(222),&szlig;,$chr(223),&agrave;,$chr(224),&aacute;,$chr(225),&acirc;,$chr(226))
  var %r = $replacecs(%r,&atilde;,$chr(227),&auml;,$chr(228),&aring;,$chr(229),&aelig;,$chr(230),&ccedil;,$chr(231),&egrave;,$chr(232),&eacute;,$chr(233),&ecirc;,$chr(234),&euml;,$chr(235))
  var %r = $replacecs(%r,&igrave;,$chr(236),&iacute;,$chr(237),&icirc;,$chr(238),&iuml;,$chr(239),&eth;,$chr(240),&ntilde;,$chr(241),&ograve;,$chr(242),&oacute;,$chr(243),&ocirc;,$chr(244))
  var %r = $replacecs(%r,&otilde;,$chr(245),&ouml;,$chr(246),&oslash;,$chr(248),&ugrave;,$chr(249),&uacute;,$chr(250),&ucirc;,$chr(251),&uuml;,$chr(252),&yacute;,$chr(253),&thorn;,$chr(254))
  var %r = $replacecs(%r,$chr(36),$,â€ž,",â€œ,",â€“,-)
  var %r = $regsubex(%r,/&#(\d+);/g,$chr(\1))
  return %r
}

;EoF
